plugins {
  id 'com.android.application'
}

def localPropsFile = rootProject.file('local.properties')
def localProps = new Properties()
if (localPropsFile.exists()) {
  localPropsFile.withInputStream(localProps.&load)
}

def stringPropertyOrDefault = { String key, String fallback ->
  if (localProps.containsKey(key)) {
    return "\"${localProps.getProperty(key)}\""
  }
  logger.lifecycle("${key} variable is not set in your local.properties")
  return "\"${fallback}\""
}

android {
  namespace 'org.smartregister.sample.anc'
  compileSdk 35
  buildToolsVersion '35.0.0'

  defaultConfig {
    applicationId 'org.smartregister.sample.anc'
    minSdk 28
    targetSdk 35
    versionCode 1
    versionName '1.0'
    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

    buildConfigField 'String', 'FLURRY_API_KEY', stringPropertyOrDefault('flurry.api.key', 'sample_key')
    buildConfigField 'String', 'OAUTH_CLIENT_ID', stringPropertyOrDefault('oauth.client.id', 'sample_client_id')
    buildConfigField 'String', 'OAUTH_CLIENT_SECRET', stringPropertyOrDefault('oauth.client.secret', 'sample_client_secret')
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
    debug {
      enableUnitTestCoverage true
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
    coreLibraryDesugaringEnabled true
  }

  packaging {
    resources {
      excludes += ['META-INF/DEPENDENCIES*', 'META-INF/LICENSE*', 'META-INF/NOTICE*', 'META-INF/INDEX.LIST', 'LICENSE.txt']
    }
  }

  lint {
    checkReleaseBuilds false
    abortOnError false
  }

  buildFeatures {
    buildConfig true
  }

  testOptions {
    unitTests.includeAndroidResources = true
  }
}

dependencies {
  implementation project(':opensrp-anc')

  implementation 'androidx.core:core:1.13.1'
  implementation 'androidx.appcompat:appcompat:1.6.1'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  implementation 'com.google.android.material:material:1.12.0'

  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

  testImplementation 'junit:junit:4.13.2'
  testImplementation 'org.hamcrest:hamcrest:2.2'

  androidTestImplementation 'androidx.test.ext:junit:1.1.5'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
